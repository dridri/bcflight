cmake_minimum_required(VERSION 2.6)

include(ProcessorCount)
ProcessorCount(NCPU)

set( VERSION_MAJOR 0 )
set( VERSION_MINOR 1 )
execute_process( COMMAND git rev-parse --short HEAD OUTPUT_VARIABLE GIT_HASH OUTPUT_STRIP_TRAILING_WHITESPACE )

SET( board "rpi" CACHE BOOL "board" )
SET( socket 1 CACHE BOOL "socket" )
SET( rawwifi 1 CACHE BOOL "rawwifi" )

option(board "board")
option(camera "camera")
option(debug "debug")
option(socket "socket")
option(rawwifi "rawwifi")

if ( NOT ${board} MATCHES OFF AND NOT ${board} MATCHES "generic" )
	set( CMAKE_TOOLCHAIN_FILE ${CMAKE_SOURCE_DIR}/../Toolchain-${board}.cmake )
endif()

enable_language( ASM )
project( flight )

macro( DIRLIST result curdir removes )
	file(GLOB children RELATIVE ${curdir} ${curdir}/*)
	set(dirlist "")
	foreach(child ${children})
		if ( NOT "${child}" MATCHES ".md" )
			string(REPLACE ".cpp" "" child ${child})
			string(REPLACE ".h" "" child ${child})
			list(APPEND dirlist ${child})
		endif()
	endforeach()
	if ( NOT ${removes} MATCHES OFF )
		list(REMOVE_ITEM dirlist ${removes})
	endif()
	list(REMOVE_DUPLICATES dirlist)
	set(${result} ${dirlist})
endmacro()

DIRLIST( BOARDS ${CMAKE_SOURCE_DIR}/boards "" )

if ( ${board} MATCHES OFF )
	message( FATAL_ERROR "You must specify a target board.\nAvailable boards are :\n${BOARDS}" )
endif()

list( FIND BOARDS ${board} board_found )
if ( ${board_found} LESS 0 )
	message( FATAL_ERROR "Board ${board} not supported.\nAvailable boards are :\n${BOARDS}" )
endif()

if ( ${board} MATCHES "rpi" )
	set( CAMERAS Raspicam )
	if ( ${camera} MATCHES OFF )
		set( camera Raspicam )
	endif()
endif()

if ( ${camera} MATCHES OFF )
# 	message( FATAL_ERROR "You must specify a camera.\nAvailable cameras for this board are :\n${CAMERAS}" )
endif()

list( FIND CAMERAS ${camera} camera_found )
if ( NOT ${camera} MATCHES OFF AND ${camera_found} LESS 0 )
	message( FATAL_ERROR "Camera ${camera} not supported.\nAvailable cameras for this board are :\n${CAMERAS}" )
endif()

include( boards/${board}/board.cmake )
add_definitions( -DBOARD="${board}" -DBOARD_${board} )
add_definitions( -DVERSION_STRING="bcflight_${board}_${VERSION_MAJOR}.${VERSION_MINOR}_${GIT_HASH}" )
if ( NOT ${camera} MATCHES OFF )
	add_definitions( -DCAMERA=${camera} -DCAMERA_INCLUDE="${camera}.h" )
endif()
add_definitions( -DBUILD_SOCKET=${socket} )
add_definitions( -DBUILD_RAWWIFI=${rawwifi} )

set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wuninitialized -std=gnu11 -fgnu89-inline" )
set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wuninitialized -std=gnu++11 -Wno-pmf-conversions -Wno-unused-result" )
set( CMAKE_LD_FLAGS "${CMAKE_LD_FLAGS} -Wall -std=gnu11" )
set( CMAKE_LINKER_FLAGS "${CMAKE_LINKER_FLAGS} -Wall -std=gnu11" )

if ( "${debug}" MATCHES "yes" OR "${debug}" MATCHES "1" )
	message( "-- WARNING : Debug enabled" )
	set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -g3" )
	set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -g3" )
	set( CMAKE_LD_FLAGS "${CMAKE_LD_FLAGS} -g3" )
	set( CMAKE_LINKER_FLAGS "${CMAKE_LINKER_FLAGS} -g3" )
else()
	set( CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -s -Os" )
	set( CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -s -Os" )
	set( CMAKE_LD_FLAGS "${CMAKE_LD_FLAGS} -s -Os" )
	set( CMAKE_LINKER_FLAGS "${CMAKE_LINKER_FLAGS} -s -Os" )
endif()

include_directories( ${CMAKE_BINARY_DIR} )
include_directories( ${CMAKE_SOURCE_DIR} )
include_directories( . )
include_directories( boards/${board} )
include_directories( stabilizer )
include_directories( console )
include_directories( frames )
include_directories( links )
include_directories( motors )
include_directories( sensors )
include_directories( peripherals )
include_directories( video )
include_directories( audio )
include_directories( ${CMAKE_SOURCE_DIR}/../external/LuaJIT-2.0.4/src )
include_directories( ${CMAKE_SOURCE_DIR}/../external/libmp4v2/include )

if ( "${rawwifi}" MATCHES 1 )
	SET( RAWWIFI_ON ${rawwifi} CACHE INTERNAL "RAWWIFI_ON" )
	include_directories( ${CMAKE_SOURCE_DIR}/../librawwifi )
	set( LIBS ${LIBS} rawwifi ${NLGENL_LIBRARY} ${NLROUTE_LIBRARY} ${NL_LIBRARY} )
endif()
# 	add_subdirectory( ${CMAKE_SOURCE_DIR}/../librawwifi ${CMAKE_BINARY_DIR}/librawwifi )
# 	include_directories( ${CMAKE_SOURCE_DIR}/../librawwifi )
# 	set( LIBS ${LIBS} ${CMAKE_BINARY_DIR}/librawwifi/librawwifi.a -lpcap ${NLGENL_LIBRARY} ${NLROUTE_LIBRARY} ${NL_LIBRARY} )
# endif()

file( GLOB_RECURSE BOARD_SOURCES boards/${board}/*.cpp boards/${board}/*.c )
file( GLOB_RECURSE CONSOLE_SOURCES console/*.cpp )
file( GLOB_RECURSE FRAMES_SOURCES frames/*.cpp )
file( GLOB_RECURSE LINKS_SOURCES links/*.cpp links/*/*.cpp links/*/*.c )
file( GLOB_RECURSE MOTORS_SOURCES motors/*.cpp )
file( GLOB_RECURSE SENSORS_SOURCES sensors/*.cpp )
file( GLOB_RECURSE PERIPHERALS_SOURCES peripherals/*.cpp )
file( GLOB_RECURSE VIDEO_SOURCES video/*.cpp )
file( GLOB_RECURSE AUDIO_SOURCES audio/*.cpp )
file( GLOB_RECURSE STABILIZER_SOURCES stabilizer/*.cpp )
set( SOURCES
	Main.cpp
	Config.cpp
	Controller.cpp
	BlackBox.cpp
	PowerThread.cpp
	Matrix.cpp
	Debug.cpp
	${CMAKE_BINARY_DIR}/flight_register.cpp )
list( APPEND SOURCES ${BOARD_SOURCES} )
list( APPEND SOURCES ${STABILIZER_SOURCES} )
list( APPEND SOURCES ${CONSOLE_SOURCES} )
list( APPEND SOURCES ${FRAMES_SOURCES} )
list( APPEND SOURCES ${LINKS_SOURCES} )
list( APPEND SOURCES ${MOTORS_SOURCES} )
list( APPEND SOURCES ${SENSORS_SOURCES} )
list( APPEND SOURCES ${PERIPHERALS_SOURCES} )
list( APPEND SOURCES ${VIDEO_SOURCES} )
list( APPEND SOURCES ${AUDIO_SOURCES} )

set_source_files_properties( ${CMAKE_BINARY_DIR}/flight_register.cpp PROPERTIES GENERATED TRUE )
add_custom_target( flight_register COMMAND ${CMAKE_SOURCE_DIR}/../tools/startup_gen.py ${CMAKE_SOURCE_DIR} ${CMAKE_BINARY_DIR}/flight_register.cpp )

add_subdirectory( ${CMAKE_SOURCE_DIR}/../libcontroller ${CMAKE_CURRENT_BINARY_DIR}/libcontroller )
include_directories( ${CMAKE_SOURCE_DIR}/../libcontroller )

add_subdirectory( ${CMAKE_SOURCE_DIR}/../libhud ${CMAKE_CURRENT_BINARY_DIR}/libhud )
include_directories( ${CMAKE_SOURCE_DIR}/../libhud )
set( LIBS ${LIBS} ${CMAKE_BINARY_DIR}/libhud/libhud.a -lpng )

if ( NOT ${TARGET_CPU_BITS} MATCHES OFF )
	set( LUA_HOST_CC_OPTIONS "-m${TARGET_CPU_BITS}" )
else()
	set( LUA_HOST_CC_OPTIONS "" )
endif()
add_custom_target( libluajit_static COMMAND bash ${CMAKE_SOURCE_DIR}/../external/build_luajit.sh ${CMAKE_BINARY_DIR} ${CMAKE_C_COMPILER} ${CMAKE_ASM_COMPILER} ${CROSS}strip ${LUA_HOST_CC_OPTIONS} )

add_executable( flight_unstripped ${SOURCES} )

add_dependencies( flight_unstripped flight_register )
add_dependencies( flight_unstripped libluajit_static )
add_dependencies( flight_unstripped hud )
add_dependencies( flight_unstripped controllerbase )
if ( ${BOARD_DEPENDENCIES} )
	add_dependencies( flight_unstripped ${BOARD_DEPENDENCIES} )
endif()
if ( "${rawwifi}" MATCHES 1 )
	add_dependencies( flight_unstripped rawwifi )
endif()

message( "LIBS : ${LIBS} ${BOARD_LIBS}" )
target_link_libraries( flight_unstripped -L./ -L${CMAKE_SOURCE_DIR}/../external/libmp4v2/build/.libs -lluajit_static -lmp4v2 controllerbase ${LIBS} ${BOARD_LIBS} )

board_strip()
